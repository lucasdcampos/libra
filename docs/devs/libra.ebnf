programa                ::= instrucao*

instrucao               ::= inst_decl_var
                          | inst_decl_func
                          | inst_decl_classe
                          | inst_atrib_var
                          | inst_atrib_indice
                          | inst_se
                          | inst_enquanto
                          | inst_romper
                          | inst_continuar
                          | inst_retornar
                          | expr

bloco                   ::= instrucao* "fim"

inst_decl_var           ::= ("var" | "const") ident (":" tipo)? "=" expr

inst_decl_classe        ::= "classe" ident (inst_decl_var | inst_decl_func)* "fim"

inst_decl_func          ::= "funcao" ident "(" (ident ("," ident)*)? ")" ("-->" tipo)? bloco

tipo                    ::= "Objeto" | "Int" | "Real" | "Texto" | "Vetor" | ident

inst_atrib_var          ::= ident "=" expr

inst_atrib_indice       ::= ident "[" expr "]" "=" expr

inst_se                 ::= "se" expr "entao" instrucao* (senao_se*)? "fim"

senao_se                ::= "senao" "se" expr "entao" instrucao*

inst_enquanto           ::= "enquanto" expr "repetir" bloco

expr                    ::= expr_lit | expr_var | expr_indice | expr_chamada | expr_unaria | expr_bin | construir_vetor

expr_lit                ::= int | real | texto

expr_var                ::= ident

expr_indice             ::= ident "[" expr "]"

expr_chamada            ::= ident "(" (expr ("," expr)*)? ")"

expr_unaria             ::= (- | !) expr

expr_bin                ::= expr op_bin expr

expr_paren              ::= "(" expr ")"

construir_vetor         ::= ("{" (expr ("," expr)*)? "}") | "[" expr? "]"

op_bin                  ::= "+" | "-" | "*" | "/" | "^" | "%" | "e" | "ou" | ">" | ">=" | "<" | "<=" | "==" | "!="

ident                   ::= letra (letra | digito | "_")*

int                     ::= digito+

real                    ::= digito+ "." digito+

texto                   ::= "\"" (caractere | escape)* "\""

letra                   ::= "a"..."z" | "A"..."Z"

digito                  ::= "0"..."9"
caractere               ::= qualquer caractere visÃ­vel exceto "\"" e "\\"

escape                  ::= "\\" ( "\"" | "\\" | "n" | "t" | "r" )
