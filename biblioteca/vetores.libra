// Biblioteca padrão da Libra
// Arquivo: vetores.libra
// Libra é distribuída pela Licença MIT, veja https://github.com/lucasdcampos/libra

// Exibe todos os elementos de um Vetor
funcao mostrarVetor(vetor: Vetor)
    var n = 0
    exibir("{ ", "")

    enquanto n < tamanho(vetor) repetir
        exibir(vetor[n], " ")
        n = n + 1
    fim

    exibir("}")
fim

// Retorna um novo Vetor com o elemento adicionado ao final
funcao incluir(vetor: Vetor, elemento: Objeto) -> Vetor
    var temp = [tamanho(vetor)+1]
    var n = 0

    enquanto n < tamanho(vetor) repetir
        temp[n] = vetor[n]
        n = n+1
    fim
    temp[n] = elemento

    retornar temp
fim

// Retorna um novo Vetor com o elemento do indice removido
funcao remover(vetor: Vetor, indice: Int) -> Vetor
    var temp = [tamanho(vetor) - 1]
    var n = 0
    var m = 0

    enquanto n < tamanho(vetor) repetir
        se n != indice entao
            temp[m] = vetor[n]
            m = m + 1
        fim
        n = n + 1
    fim

    retornar temp
fim

// Retorna se o Vetor está vazio
funcao estaVazio(vetor: Vetor) -> Int
    retornar tamanho(vetor) == 0
fim

// Retorna um novo Vetor com o elemento adicionado ao inicio
funcao adicionarInicio(vetor: Vetor, elemento: Objeto) -> Vetor
    var temp = [tamanho(vetor) + 1]
    temp[0] = elemento
    var n = 0

    enquanto n < tamanho(vetor) repetir
        temp[n + 1] = vetor[n]
        n = n + 1
    fim

    retornar temp
fim

// Retorna o reverso de um Vetor
funcao reverter(vetor: Vetor) -> Vetor
    var temp = [tamanho(vetor)]
    var n = 0
    
    enquanto n < tamanho(vetor) repetir
        temp[n] = vetor[(tamanho(vetor) - 1) - n]
        n = n + 1
    fim

    retornar temp
fim

// Retorna o maior elemento de um Vetor
funcao maiorElemento(vetor: Vetor)
    se tamanho(vetor) == 0 entao
        retornar nulo
    fim

    var maior = vetor[0]
    var n = 1

    enquanto n < tamanho(vetor) repetir
        se vetor[n] > maior entao
            maior = vetor[n]
        fim
        n = n + 1
    fim

    retornar maior
fim

funcao ordenar(vetor: Vetor) -> Vetor
    var i = 0
    enquanto i < tamanho(vetor) repetir
        var j = 0
        enquanto j < tamanho(vetor) - 1 repetir
            se vetor[j] > vetor[j + 1] entao
                var temp = vetor[j]
                vetor[j] = vetor[j + 1]
                vetor[j + 1] = temp
            fim
            j = j + 1
        fim
        i = i + 1
    fim
    retornar vetor
fim